#!/usr/bin/env perl

use strict;
use warnings;
use autodie;

use Apache::Log::Parser;

my $file = shift;

open my $in, '<', $file;
open my $out, '>', "$file.stripped";

my $parser = Apache::Log::Parser->new( fast => 1 );

while (<$in>) {
    my $record = $parser->parse($_);

    # MFA was hot-linking this image from their blog
    next if $record->{request} && $record->{request} =~ m{\Q/images/mfa-logo.png};
    $record->{referer} = q{-};

    # There are some weird entries in the vegguide.org logs, apparently.
    next unless $record->{datetime};

    print {$out} join q{ }, @{$record}{qw( rhost logname user )},
        _bracket( $record->{datetime} ),
        _quote( $record->{request} ), @{$record}{qw( status bytes )},
        _quote( $record->{referer} ),
        _quote( $record->{agent} );
    print {$out} "\n";
}

close $out;

sub _bracket {
    return '[' . $_[0] . ']';
}

sub _quote {
    return q{"} . $_[0] . q{"};
}
